# --- Day 7: Laboratories ---

```elixir
Mix.install([{:kino_aoc, "~> 0.1"}])
```

## Setup

<!-- livebook:{"attrs":"eyJhc3NpZ25fdG8iOiJwdXp6bGVfaW5wdXQiLCJkYXkiOiI3Iiwic2Vzc2lvbl9zZWNyZXQiOiJBT0NfU0VTU0lPTl9DT09LSUUiLCJ5ZWFyIjoiMjAyNSJ9","chunks":null,"kind":"Elixir.KinoAOC.HelperCell","livebook_object":"smart_cell"} -->

```elixir
{:ok, puzzle_input} =
  KinoAOC.download_puzzle("2025", "7", System.fetch_env!("LB_AOC_SESSION_COOKIE"))
```

```elixir
test_input = Kino.Input.textarea("test_input")
```

```elixir
test_input = Kino.Input.read(test_input)
```

## Part 1

```elixir
[first_row | rows] = String.split(puzzle_input)
[{starting_index, _}] = Regex.run(~r(S), first_row, return: :index)

rows
|> Enum.map(fn row ->
  ~r/\^/
  |> Regex.scan(row, return: :index)
  |> Enum.map(fn [{index, _}] -> index end)
  |> MapSet.new()
end)
|> Enum.reject(&(MapSet.size(&1) == 0))
|> Enum.reduce({MapSet.new([starting_index]), 0}, fn next_splitter_row, {beam_paths, splits} ->
  beam_collisions = MapSet.intersection(beam_paths, next_splitter_row)
  new_splitter_paths = beam_collisions |> Enum.flat_map(&[&1 - 1, &1 + 1]) |> MapSet.new()

  new_beam_paths =
    beam_paths |> MapSet.difference(next_splitter_row) |> MapSet.union(new_splitter_paths)

  {new_beam_paths, splits + MapSet.size(beam_collisions)}
end)
|> elem(1)
```

## Part 2

```elixir
[first_row | rows] = String.split(puzzle_input)
[{starting_index, _}] = Regex.run(~r(S), first_row, return: :index)

rows
|> Enum.map(fn row ->
  ~r/\^/
  |> Regex.scan(row, return: :index)
  |> Enum.map(fn [{index, _}] -> index end)
end)
|> Enum.reject(&(&1 == []))
|> Enum.reduce(%{starting_index => 1}, fn splitter_indexes, beams ->
  for {index, value} <- beams, reduce: beams do
    acc ->
      if index in splitter_indexes do
        left_split = index - 1
        right_split = index + 1

        acc
        |> Map.update(left_split, value, &(&1 + value))
        |> Map.update(right_split, value, &(&1 + value))
        |> Map.delete(index)
      else
        acc
      end
  end
end)
|> Map.values()
|> Enum.sum()
```

<!-- livebook:{"offset":2080,"stamp":{"token":"XCP.A8LGRIiw_MK_qYQQ7mEirqdzEDdFxHPFWMnsnAuOmZt3lpdJeCZgeTW5gHt-e0ICXX_4tp-vVsGNuO8rad6r3WAgJ3It5Hsn5M1y9yC1BfW4ImTmIDPvATNp2h0n","version":2}} -->
